FUNCTION_BLOCK WebSpider

	VAR_INPUT 
		position : REAL;
		frequency : REAL;
		distance : REAL;
	END_VAR
	VAR_OUTPUT 
		score : REAL;
	END_VAR
	
	FUZZIFY position
		TERM out_web :=(0,1) (2,1) (2,0);
		TERM in_text := (2,0) (2,1) (6,1) (7,0); 
		TERM in_title := gauss 8 2;
		TERM in_both := (9,0) (10,1);
	END_FUZZIFY
	
	FUZZIFY frequency
		TERM low := (0,0) (0,1) (4, 0); 
		TERM medium := (3, 0) (4, 1) (10,1) (20,0);
		TERM high := gauss 20 5;
		TERM extreme := (20,0) (100,1) (300,1) (300,0);
	END_FUZZIFY
	
	FUZZIFY distance
		TERM near :=(0,1) (20,1) (30,0);
		TERM far := (20,0) (30,1) (1000,1) (1000,0); 
	END_FUZZIFY
	
	DEFUZZIFY score
		TERM extreme_low := (0,0) (0, 1) (10, 1) (15, 0);
		TERM low := gauss 20 5;
		TERM normal := gbell 20 4 50; 
		TERM high := gauss 80 5;
		TERM extreme_high := (85,0)(86,0.1)(88,0.6)(89,0.9)(90,1)(100,1)(100,0);
		METHOD : COG;
		DEFAULT := 0;
	END_DEFUZZIFY
	
	RULEBLOCK No1
		AND : MIN;
		ACT : MIN;
		ACCU : MAX;
		RULE 1 : IF position IS out_web THEN score IS extreme_low;
		RULE 2 : IF frequency IS low THEN score IS low;
		RULE 3 : IF frequency IS medium AND position IS in_text THEN score IS extreme_low;
		RULE 4 : IF frequency IS medium AND position IS in_title THEN score IS low;
		RULE 5 : IF frequency IS medium AND position IS in_both THEN score IS normal;
		RULE 6 : IF frequency IS high AND position IS NOT out_web AND position IS NOT in_text THEN score IS high;
		RULE 7 : IF frequency IS high AND distance IS far THEN score IS normal;
		RULE 8 : IF frequency IS high AND distance IS near THEN score IS high;
		RULE 9 : IF frequency IS extreme AND distance IS far OR frequency IS extreme AND position IS NOT in_both THEN score IS high;
		RULE 10 : IF frequency IS extreme AND distance IS near AND position IS in_both THEN score IS extreme_high;
	END_RULEBLOCK
END_FUNCTION_BLOCK